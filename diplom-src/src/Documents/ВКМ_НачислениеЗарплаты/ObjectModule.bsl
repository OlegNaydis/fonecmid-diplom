
Процедура ОбработкаПроведения(Отказ, Режим)   
	
		//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_НачислениеЗарплатыНачисления.Сотрудник КАК Сотрудник,
		|	ВКМ_НачислениеЗарплатыНачисления.ВидРасчета КАК ВидРасчета,
		|	ВКМ_НачислениеЗарплатыНачисления.ДатаНачала КАК ДатаНачала,
		|	ВКМ_НачислениеЗарплатыНачисления.ДатаОкончания КАК ДатаОкончания,
		|	ЕСТЬNULL(ВКМ_УсловияОплатыСотрудниковСрезПоследних.Оклад, 0) КАК Оклад
		|ИЗ
		|	Документ.ВКМ_НачислениеЗарплаты.Начисления КАК ВКМ_НачислениеЗарплатыНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Дата, ) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
		|		ПО ВКМ_НачислениеЗарплатыНачисления.Сотрудник = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник
		|ГДЕ
		|	ВКМ_НачислениеЗарплатыНачисления.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Дата",Дата);
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = Выборка.ВидРасчета;
		Движение.ПериодДействияНачало = Выборка.ДатаНачала;
		Движение.ПериодДействияКонец = КонецДня(Выборка.ДатаОкончания);
		Движение.ПериодРегистрации = Дата;     
		Если Выборка.ВидРасчета=ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск тогда
			Движение.БазовыйПериодНачало = ДобавитьМесяц(НачалоДня(Дата),-13);
			Движение.БазовыйПериодКонец  = НачалоДня(Дата)-1;  
		КонецЕсли;
		Движение.Сотрудник = Выборка.Сотрудник; 
		Если Выборка.ВидРасчета=ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад тогда
			Движение.Показатель= Выборка.Оклад;                                         
		КонецЕсли;
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать();
	
	РассчитатьОклад(Движения.ВКМ_ОсновныеНачисления); 
	
	РассчитатьПроцент(); 
	
	ВзаиморасчетыССотрудниками();
КонецПроцедуры     


Процедура ВзаиморасчетыССотрудниками()
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ВКМ_ОсновныеНачисления.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
		|ГДЕ
		|	ВКМ_ОсновныеНачисления.Регистратор = &Регистратор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВКМ_ДополнительныеНачисления.Сотрудник,
		|	ВКМ_ДополнительныеНачисления.Сумма
		|ИЗ
		|	РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
		|ГДЕ
		|	ВКМ_ДополнительныеНачисления.Регистратор = &Регистратор
		|
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.Сотрудник КАК Сотрудник,
		|	СУММА(ВТ.Сумма) КАК Сумма
		|ИЗ
		|	ВТ КАК ВТ
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ.Сотрудник";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Движения.ВКМ_РасчетыССотрудниками.Записывать = Истина;  
	Движения.ВКМ_Удержания.Записывать = Истина;
	
	Пока Выборка.Следующий() Цикл    
		Движение = Движения.ВКМ_РасчетыССотрудниками.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Сотрудник = Выборка.Сотрудник; 
		Движение.Сумма = Выборка.Сумма;  
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	
КонецПроцедуры	

Процедура РассчитатьОклад(НаборЗаписей)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеГрафика.НомерСтроки КАК НомерСтроки,
	|	ДанныеГрафика.ВидРасчета КАК ВидРасчета,
	|	ЕСТЬNULL(ДанныеГрафика.ЗначениеФактическийПериодДействия, 0) КАК ОтработаноЧасов,
	|	ЕСТЬNULL(ДанныеГрафика.ЗначениеПериодДействия, 0) КАК НормаЧасов
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(
	|			Регистратор = &Регистратор
	|				И ВидРасчета = &Оклад) КАК ДанныеГрафика";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);      
	Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПоляПоиска = Новый Структура("НомерСтроки");
	
	Для Каждого Запись Из НаборЗаписей Цикл
		
		ПоляПоиска.НомерСтроки = Запись.НомерСтроки;
		
		Если Выборка.НайтиСледующий(ПоляПоиска) Тогда
			Запись.Сумма = ?(Выборка.НормаЧасов = 0, 0, Запись.Показатель * Выборка.ОтработаноЧасов / Выборка.НормаЧасов);
		КонецЕсли; 
		Выборка.Сбросить();
	КонецЦикла; 
	НаборЗаписей.Записать(,,Ложь);
	
КонецПроцедуры       



Процедура РассчитатьПроцент()
    //найдем сдельную ЗП для всех сотрудников, упомянутых в документе   
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ВыполненныеСотрудникомРаботыОбороты.Сотрудник КАК Сотрудник,
	|	ВКМ_ВыполненныеСотрудникомРаботыОбороты.СуммаОборот КАК СуммаОборот
	|ИЗ
	|	РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(
	|			&НачДата,
	|			&КонДата,
	|			,
	|			Сотрудник В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					ВКМ_НачислениеЗарплатыНачисления.Сотрудник КАК Сотрудник
	|				ИЗ
	|					Документ.ВКМ_НачислениеЗарплаты.Начисления КАК ВКМ_НачислениеЗарплатыНачисления
	|				ГДЕ
	|					ВКМ_НачислениеЗарплатыНачисления.Ссылка = &Ссылка)) КАК ВКМ_ВыполненныеСотрудникомРаботыОбороты";  
	
	Запрос.УстановитьПараметр("НачДата",НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонДата",КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.СдельныйПроцент;
		Движение.ПериодРегистрации = Дата;     
		//Движение.БазовыйПериодНачало = НачалоМесяца(Дата);
		//Движение.БазовыйПериодКонец  = КонецМесяца(Дата);  
		Движение.Сотрудник = Выборка.Сотрудник; 
		Движение.Сумма= Выборка.СуммаОборот;                                         
	КонецЦикла;
	
	Движения.ВКМ_ДополнительныеНачисления.Записать();
	

КонецПроцедуры
